\chapter{Logische Programmierung (Prolog)}
Das Programmierparadigma von Prolog ist \textbf{deklarativ-logisch}. Prolog ist ein Akronym für PROgrammation en LOGique. Wir beschreiben mit Logik WAS wir wollen und nicht WIE wir es berechnen. Die wichtigsten Mechanismen in Prolog sind \textbf{Matching} und \textbf{automatisches Backtracking}. Zentral ist die \textbf{Wissensdatenbank}, worin alle \textbf{Fakten \& Regeln} stecken. Die Wissensdatenbank kann angefragt werden, wobei Prolog darauf entsprechend antwortet.

\begin{figure}[h!]
\centering
\includegraphics[width=0.7\linewidth]{fig/prolog-funktionsweise}
\caption{Funktionsweise Prolog}
\label{fig:funktionsweise-prolog}
\end{figure}

\begin{lstlisting}[caption=Wissensdatebank mit nur Fakten]
% In folgender Wissensdatenbank sind drei Fakten enthalten. Bigger definiert, dass
% das erste Elemente grösser als das zweite Element ist.
bigger(elephant, horse).
bigger(horse, dog).
bigger(horse, sheep).

% Gegen die Wissensdatenbank kann eine Anfrage gemacht werden. Prolog prüft ob es 
% für die Anfrage einen Match gibt. Jedoch existiert aber keine Match dafür. 
% Nirgends ist definiert, dass ein Hund grösser als ein Elefant ist.
?- bigger(dog, elephant).
false.

% Für folgende Anfrage ist ein Match vorhanden. Fakt 1 in der Wissensdatenbank 
% matcht die Anfrage.
?- bigger(elephant, horse).
true.

% Bei Anfragen können auch Variabeln definiert werden. Alle gross geschriebenen 
% Wörter sind Variabeln. Prolog sucht nun nach allen Matches, welche als erstes 
% Element horse haben. Als Resultat werden alle möglichen X Werte geliefert. (Prolog 
% liefert immer nur ein Resultat - möchte man weitere Resultate ansehen, muss 
% Semikolon gedrückt und um die Abfrage zu beenden den Punkt Operator wählen.)
?- bigger(horse, X).
X = dog ;
X = sheep ;
false.
\end{lstlisting}

\newpage

\begin{lstlisting}[caption=Transitive Hülle von \emph{bigger/2}]
% Wir wissen implizit, dass der Elefant grösser ist als der Hund. Da der Elefant
% grösser als das Pferd ist und das Pferd grösser als der Hund. Aber eine Anfrage ob 
% der Elefant nun grösser als der Hund ist, würde false liefern. Es müssen Regeln 
% definiert werden, welche diese Beziehung unter den Fakten abbilden. Wir definieren 
% für bigger/2 eine transitive Hülle.
is_bigger(X, Y) :- bigger(X, Y).
is_bigger(X, Y) :- bigger(X, Z), is_bigger(Z, Y).

?- is_bigger(elephant, dog).
true.

% Selbstverständlich können nun auch Anfragen mit Variabeln über die transitive 
% Hülle durchgeführt werden.
?- is_bigger(X, dog).
X = horse ;
X = elephant ;
false.

% Es können auch verknüpfte Anfragen (UND) ausgeführt werden.
?- is_bigger(elephant, X), is_bigger(X, dog).
X = horse ;
false.

% Eine is_smaller/2-Regel ist schnell implementiert.
is_smaller(X, Y) :- is_bigger(Y, X).
\end{lstlisting}

\section{Syntax}
In Prolog gibt es verschiedene Typen von Termen.

\begin{figure}[h!]
\centering
\includegraphics[width=0.5\linewidth]{fig/prolog-terme}
\caption{Übersicht Terme in Prolog}
\label{fig:prolog-terme}
\end{figure}

\begin{description}
	\item[Zahlen] Gelten auch als atomare Terme: \verb|123|, \verb|4657.8|, \verb|-9|
	
	\item[Atome] Beginnen mit Kleinbuchstaben oder sind in einfachen Anführungszeichen eingeschlossen. Gelten auch als atomare Terme: \verb|elephant|, \verb|'Mein Text'|
	
	\item[Zusammengesetzte Terme] Bestehen aus dem Funktor und Argumenten. Der Funktor ist ein Atom (\verb|is_bigger|) und die Argumente sind Terme: \verb|is_bigger(horse, X)|
	
	\item[Variablen] Beginnen mit Grossbuchstaben oder einem Unterstrich: \verb|X|, \verb|Elephant|, \verb|_elephant|
	
	\item[Anonyme Variable] Die Variable \verb|_| (Unterstrich) heisst \textbf{anonyme Variable}. Diese dient als Platzhalter, dessen Wert nicht interessiert. Jedes auftreten repräsentiert eine neue Variable. 
		
	\item[Grundterme] Terme ohne Variablen. Diese sind als sogenannte Fakten bekannt: \verb|bigger(you, me)|, \verb|write('hello')|
	
	\item[Prädikate] Atome und zusammengesetzte Terme. Treten sie als Atome auf sind es Fakten (\verb|bigger|) sonst sind es Regeln \verb|is_bigger(X, Y) :- bigger(X, Y)|.
	
	\item[Stelligkeit] Die Stelligkeit eines Prädikats zeigt auf wie viele Argumente das Prädikat entgegennimmt. Die Stelligkeit wird am Schluss des Prädikats angegeben: \verb|is_bigger/2|
	
	\item[Anfragen] sind Prädikate oder Sequenzen von Prädikaten gefolgt von einem Punkt. Der Punkt veranlasst Prolog zu antworten.
	
	\item[Klauseln] sind Fakten und Regeln (zusammengesetzte Terme).
	
	\item[Prozedur] Alle Klauseln zum gleichen Prädikat (d.h alle Relationen mit gleichem Name (Funktor) \& Stelligkeit).
	
	\item[Prolog-Programm] Liste aller Klauseln.
	
	\item[Fakten] Typischerweise Grundterme.
	
	\item[Regeln] Bestehen aus einem Kopf (head) und einem Hauptteil (body), welche durch \verb|:-| getrennt sind. Der Kopf einer Regel ist wahr, wenn alle Ziele (Prädikate) im Hauptteil als wahr bewiesen werden. Ziele werden durch Komma getrennt, welches eine UND-Verknüpfung darstellt (Konjunktion).
\end{description}

Prolog nutzt Hornklauseln um die Regeln auszuwerten. Die Definition der Hornklausel $(p_1 \wedge p_2 \wedge ... \wedge p_n \Rightarrow q)$ kann auf die Prolog-Syntax \verb|q :- p1, p2, ... pn.| übertragen werden.

\section{Matching}

Wenn Anfragen an das Prolog-System gestellt werden, führt Prolog eine Beweissuche mittels Backtracking und Matching durch. Zwei Terme matchen, wenn sie identisch sind oder wenn sie durch ersetzen von Variabeln durch andere Terme identisch gemacht werden können. Mittels dem Gleichheits-Prädikat \verb|=/2| kann abgefragt werden, ob zwei Terme matchen.

\begin{lstlisting}[caption=Gleichheits-Prädikat]
?- eat(tom, burger) = eat(X, burger).
X = tom.
?- eat(tom, burger) = eat(X, potato).
false.

% Atome matchen, wenn sie genau gleich sind.
?- =(tom, tom). % Predefined predicate =/2
true.
?- tom = tom. % other style: =/2 as infix operator
true.
?- 'Tom' = tom. % Watch out: case sensitive!
false.
?- 'tom' = tom. % =/2 ignores single quotes
true.

% Falls ein Term eine Variable ist, dann matchen beide Terme und die Variable wird 
% dem Wert des zweiten Terms instanziiert.
?- pia = X.
X = pia.
?- sing(pia) = X.
X = sing(pia).
?- X = Y.
X = Y.
?- X = Y, X = pia.
X = Y, Y = pia.
?- X = Y, X = pia, Y = tom.
false.

% Matchen wenn gleicher Funktor, gleiche Stelligkeit und alle korrespondierende 
% Argumente matchen.
?- meet(drink(pia), eat(X)) = meet(Y, eat(tom)).
X = tom,
Y = drink(pia).
?- meet(X, X) = meet(drink(pia), tom).
false.
?- meet(pia, tom) = meet(Y).
false.
\end{lstlisting}

Man könnte auch unendliche Terme definieren (z.B. \verb|f(A) = A|). Diesen Term könnte man mit \textit{Unifikation} (kommt aus Logik, ähnlich wie Matching in Prolog) auflösen. Das korrekte Resultat müsste dann \verb|A = f(f(..))| sein. Prolog gibt aber das Resultat \verb|A = f(A)| aus. Das liegt daran dass in Prolog standardmässig kein \textit{Occurs Check} durchgeführt wird. Beim Occurs Check wird überprüft ob die Variable im Term vorkommt.

\section{Beweissuche und Suchbäume}
Das Prolog-System versucht die Anfrage zu beweisen. Dazu versucht es die Fakten und Regeln in der Wissensdatenbank zu matchen. Dabei wird ein Backtracking-Algorithmus verwendet, welcher eine Tiefensuche darstellt. Falls die Anfrage aufgelöst werden kann, liefert es true oder gegebenenfalls die Matchings zurück. Ansonsten false. Die Anordnung der Regel und Fakten in der Wissensdatenbank beeinflusst wie schnell Prolog zu einer Lösung kommt. Folgende Regeln werden bei der Beweissuche angewendet:

\begin{itemize}
	\item Suche in der Wissensdatenbank von oben nach unten
	\item Abarbeitung der Abfrage-Klauseln von links nach rechts
	\item Backtracking zur Erholung von „schlechten“ Pfaden
\end{itemize}

\begin{figure}[h!]
\centering
\includegraphics[width=0.47\linewidth]{fig/prolog-beweissuche-suchbaueme}
\caption{Prolog: Beweissuche und Suchbäume}
\label{fig:prolog-beweissuche-suchbaueme}
\end{figure}

\section{Deklarative vs. Prozedurale Bedeutung}
Grundsätzlich reicht es in Prolog die deklarative Bedeutung (WAS - Problem-Beschreibung - deklarativ-logisch) anzugeben und es muss nicht die prozedurale Bedeutung (WIE - Beschreibung der Problemlösung - imperativ) definiert werden. Der deklarative Weg reicht leider nicht immer. Aus Ausführungseffizienzgründen gibt es in Prolog prozedurale Konstrukte wie Endrekursion, Assertions, Cut-Operator \& Negation. Conclusion: Wenn das Problem modelliert ist, ist es (grundsätzlich) gelöst. Einzige Einschränkung ist die Effizienz, welches in der prozeduralen Programmierung gesteuert werden kann. Oft es auch nicht einfach das Problem zu modellieren. Übung macht den Meister wie bei guter OO-Modellierung.

\newpage

\section{Typische Prolog-Poblemstellungen}
Mit Prolog lassen sich auf nette Art und Weise folgende Problemstellungen lösen: 

\begin{itemize}
	\item Kreuzworträtsel
	\item Färben von Karten
	\item Zahlenrätsel
	\item Sudoku
	\item Spracherkennung
\end{itemize}

\subsection{Kreuzworträtsel}
\begin{enumerate}
	\item Jedes leere Feld muss mit einem Identifier markiert werden, welcher als Platzhalter für einen Buchstaben dient.
	\item  In der Wissensdatenbank müssen alle möglichen Wörter definiert werden.
	\item Regeln für alle Wörter aus dem Kreuzworträtsel definieren.
	\item Anfrage formulieren und ausführen.
\end{enumerate}

\begin{figure}[h!]
\centering
\includegraphics[width=1\linewidth]{fig/prolog-kreuzwortraetsel}
\caption{Prolog: Kreuzworträtsel lösen}
\label{fig:prolog-kreuzwortraetsel}
\end{figure}

\subsection{Karten färben}
Es liegt eine fixe Anzahl an unterschiedlichen Farben vor. Nun sollen die Länder auf einer Karte eingefärbt werden, wobei angrenzende Länder unterschiedlich gefärbt sein sollen. 
\begin{enumerate}
	\item Definition von Prädikaten, welche Farben nebeneinander sein dürfen.
	\item Definition von Ländern, welche nebeneinander liegen. Da diese alle um die gleiche Zeit erfüllt sein müssen, werden diese als Konjunktion definiert.
\end{enumerate}

\begin{figure}[h!]
\centering
\includegraphics[width=1\linewidth]{fig/prolog-karten-faerben}
\caption{Prolog: Karten färben}
\label{fig:prolog-karten-faerben}
\end{figure}

\newpage
\section{Operatoren und Prädikate}
Operatoren und Prädikate sind dasselbe. Es sind einfach zwei unterschiedliche Schreibweisen. Grundsätzlich gibt es nur Prädikate, welche aber auch als Operatoren verwendet werden könne.

\begin{lstlisting}[caption=Prädikat >/2]
?- 3 > 7. % infix operator style for predicate >/2
false.
?- >(3, 7). % >/2 predicate, "regular style"
false.
\end{lstlisting}

Mittels dem Prädikat \verb|op(+Precedence, +Type, :Name)| können Prädikate als Operatoren deklariert werden.

\begin{lstlisting}[caption=is\_bigger/2 als Operator]
?- op(1150, xfx, is_bigger). % declare new operation
true.
?- elephant is_bigger dog. % use our new operation
true.
\end{lstlisting}

Die \textbf{Präzedenz} (Operatorrangfolge) von einem Operator definiert, wie stark ein Operator seine Operanden bindet. Eine tiefere Präzedenz bedeutet eine stärkere Bindung. Dadurch wird definiert, wann welcher Operator auf seine Operanden angewendet wird. In SWI-Prolog wird dieser Wert zwischen 1 und 1200 angegeben. Der Haupt-Funktor von einem Term ist der Operator mit der höchsten Präzedenz. Der \textbf{Operator-Typ} definiert die relative Reihenfolge von Operator f und Operanden x und y. Es gibt die folgenden drei Typen:

\begin{description}
	\item[Infix] xfx, xfy, yfx. Operator ist zwischen den beiden Operanden.
	\item[Präfix] fx, fy. Operator f ist vor dem Operand.
	\item[Postfix] xf, yf. Operator f ist nach dem Operand.	
\end{description}

Mit x und y wird die Präzedenz der Operanden einer Operation f festgelegt. X repräsentiert einen Operanden, dessen Präzedenz strickt kleiner ist als die Präzedenz vom Operator f. Y repräsentiert einen Operanden, dessen Präzedenz kleiner oder gleich derjenige vom Operator f ist. Dies dient schlussendlich dazu, dass auch folgendes korrekt ausgewertet wird: \emph{1 - 2 - 3 }. 

\begin{figure}[h!]
\centering
\includegraphics[width=0.7\linewidth]{fig/prolog-operatoren}
\caption{Prolog: Operatoren}
\label{fig:prolog-operatoren}
\end{figure}

\newpage
In der API-Dokumentation von Prolog werden folgende Zeichen eingesetzt:
\begin{description}
	\item[-] Operand sollte ungebunden (d.h. nicht instanziiert) sein. Also eine Variable (Wert wird durch Prädikat zugewiesen).
	\item[+] Operand sollte gebunden (d.h. instanziiert) sein. Also eine Zahl, ein Atom oder ein gebundener zusammengesetzter Term (Term wird vom Prädikat gebraucht).
	\item[?] Operand kann gebunden oder ungebunden sein.
\end{description}

\subsection{Arithmetische Operatoren}
\verb|+| Addition, \verb|-| Subtraktion, \verb|*| Multiplikation, \verb|/| Division, \verb|**| Potenz, \verb|//| Ganzzahldivision, \verb|mod| Modulo (Rest bei Ganzzahldivision). Wenn versucht wird \verb|X = 1 + 2| zu berechnen, gelingt dies nicht, da ein Matching ausgeführt wird. Dafür muss der \verb|is/2|-Operator verwendet werden (\verb|X is 1 + 2|. Der Ausdruck \verb|8 is X| würde ein Runtime-Fehler geben.

\begin{lstlisting}[caption=Arithmetische Terme]
?- D is 5/2. % division
D = 2.5.
?- I is 5//2. % integer division
I = 2.
?- Z is 5 mod 2. % modulo (remainder of integer div.)
Z = 1.
?- P is 2 ** 4. % power
P = 16.
?- C is cos(0). % cosine
C = 1.0.
?- S is sqrt(9). % square root
S = 3.0.
\end{lstlisting}

\subsection{Vergleichsoperatoren}
\verb|>| grösser als, \verb|<| kleiner als, \verb|>=| grösser-gleich, \verb|=<| kleiner-gleich, \verb|=:=| Gleichheit, \verb|=\=| Ungleichheit.

\begin{lstlisting}[caption=Terme mit Vergleichen]
?- 55 > 6. % is 55 is greater than 6?
true.
?- 88 =< 77. % is 88 smaller or equal than 77?
false.
?- 1 + 2 = 2 + 1. % Attention: matching!
false. % 1+2 does not match 2+1
?- 1 + 2 =:= 2 + 1. % is 1+2 equal to 2+1?
true.
?- 44 =\= 42 + 1. % is 44 unequal to 42+1?
true.
\end{lstlisting}

\newpage

\section{Rekursion}
Prolog Prädikate können rekursiv definiert sein. Ein Prädikat ist rekursiv definiert, falls sich eine oder mehrere Regeln in ihrer Definition auf sich selber beziehen. Dies wird in Prolog häufig eingesetzt.

\begin{lstlisting}[caption=Beispiel Rekursion]
is_bigger(X, Y) :- bigger(X, Y). % simple case - Rekursionbasis
is_bigger(X, Y) :- bigger(X, Z), is_bigger(Z, Y). % general case - Rekursionsvor.
\end{lstlisting}

\begin{figure}[h!]
\centering
\includegraphics[width=0.7\linewidth]{fig/prolog-suchbaum-rekursion}
\caption{Prolog: Rekursions-Suchbaum}
\label{fig:prolog-suchbaum-rekursion}
\end{figure}

\begin{lstlisting}[caption=Fakultät]
fak(0, 1). 				% simple case
fak(N, F) :- 			% general case
	N > 0, 				% argument test
	N1 is N - 1, 		% evaluate N-1
	fak(N1, F1), 		% recursive call
	F is N * F1. 		% sum up

?- fak(5, X). X = 120 .
?- fak(4, 24). true.
?- fak(X, 24). ERROR: >/2: Arguments are not sufficiently instantiated
\end{lstlisting}

\begin{lstlisting}[caption=Fibonacci, label=lst:naiv-fib]
% Naive Implementierung! Optimierung mittels Endrekursion und Assertions.
fib(0, 0).
fib(1, 1).
fib(N, F) :-
	N > 1,
	N1 is N - 1,
	N2 is N - 2,
	fib(N1, F1),
	fib(N2, F2),
	F is F1 + F2.

?- fib(7, X). X = 13 .
?- fib(6, 8). true .
?- fib(X, 8). ERROR: >/2: Arguments are not sufficiently instantiated (Da brauchen wir CLP!)
\end{lstlisting}

\section{Optimierungen}

Backtracking ist grundsätzlich nicht sehr effizient. Wir können. Um dies zu entschärfen gibt es zwei bekannte Methoden: Endrekursion und Assertions.

\subsection{Optimierung durch Endrekursion}
Die Endrekursion ist auch bekannt als \emph{tail recursion}. Der Vorteil ist, dass kein Backtracking benötigt wird. Eine Endrekursion kann als Iteration ohne zusätzlichen Speicherplatz ausgeführt werden. Eine Prozedur ist endrekursiv, wenn:

\begin{itemize}
	\item Nur ein einzigen rekursiven Aufruf hat.
	\item Dieser Aufruf ist der letzte Aufruf in der letzten Klausel der Prozedur.
	\item Alle Aufrufe vor dem rekursiven Aufruf müssen deterministisch sein.
\end{itemize}

Prolog führt folgende endrekursive Prozedur als Iteration aus. Es wird viel weniger Speicherplatz benötigt. Diese Optimierung nennt sich auch \emph{last call optimization}.

\begin{lstlisting}[caption=Endrekursion: Allg. Beispiel]
p(...) :- ... 	% no recursive call in the body
p(...) :- ... 	% no recursive call in the body
...
p(...) :- ..., 	% all deterministic and
	..., 		% no recursive call until here
	p(...). 	% tail-recursive call
\end{lstlisting}

Im Listing \ref{lst:naiv-fib} ist die rekursive Berechnung alles andere als effizient. Möchte man die Fibonacci Zahl für 30 berechnen, führt dies auf herkömmlichen Rechnern zu einem \emph{ERROR: Out of local stack}. Das Problem ist, dass viele Zwischenresultate mehrmals berechnet werden müssen. Für \emph{f(6)} muss 5x \emph{f(2)} ausgewertet werden. Die Umwandlung von einem naivem Prolog-Programm in ein endrekursives ist nicht trivial! Typischerweise werden zusätzlich neue Argumente verwendet: Akkumulatoren. Diese werden verwendet um Zwischenresultate zu speichern.

\begin{lstlisting}[caption=Endrekursive Fibonnaci-Berechnung]
fib_tr(N, F) :- fib_tr(N, 0, 1, F). % call accumulator
fib_tr(0, A, _, A). 				% simple case
fib_tr(N, A, B, F) :- 				% general case
	N1 is N - 1, 					% new argument N1
	N1 >= 0, 						% avoid underflow
	Sum is A + B,					% accumulator Sum
	fib_tr(N1, B, Sum, F). 			% tail-recursisve call
\end{lstlisting}

Fazit: Falls der Speicherplatz bei einer rekursiven Prozedur kritisch wird, hilft die Umwandlung in ein endrekursive Prozedur. Diese kann als Iteration ausgeführt werden, benötigt oft weitere Argumente: Akkumulatoren.

\newpage
\subsection{Optimierung durch Assertions (Memoization)}
Die Wissensdatenbank kann nicht nur statisch sondern auch dynamisch zur Laufzeit manipuliert werden. Dies kann für Memoization (auch Caching) genutzt werden.

\begin{lstlisting}[caption=dynamic/1]
% Damit Prädikate zur Laufzeit modifiziert werden können, müssen diese mit dynamic definiert werden.
:- dynamic bigger/2.
bigger(elephant, horse).
bigger(horse, dog).
bigger(horse, sheep).
\end{lstlisting}

\begin{lstlisting}[caption=listing/1]
% Mittels listing können die Fakten und Regel zu einem Prädikat angezeigt werden.
?- listing(bigger).
:- dynamic bigger/2.
bigger(elephant, horse).
bigger(horse, dog).
bigger(horse, sheep).
\end{lstlisting}

\begin{lstlisting}[caption=asserta/1 \& assertz/1]
% Asserta fügt einen neuen Fakt an den Beginn hinzu.
?- asserta(bigger(me, you)).
true.
?- listing(bigger).
:- dynamic bigger/2.
bigger(me, you). % new fact as first rule
bigger(elephant, horse).
bigger(horse, dog).
bigger(horse, sheep)..

% Assertz fügt einen neuen Fakt an den Schluss hinzu.
?- assertz(bigger(elephant, me)).
true.
?- listing(bigger).
:- dynamic bigger/2.
bigger(me, you).
bigger(elephant, horse).
bigger(horse, dog).
bigger(horse, sheep).
bigger(elephant, me). % new fact asserted as last rule
\end{lstlisting}

\begin{lstlisting}[caption=retract/1]
% Mittels retract können Fakten/Regeln entfernt werden.
?- retract(bigger(me, you)).
true.
?- listing(bigger).
:- dynamic bigger/2.
bigger(elephant, horse).
bigger(horse, dog).
bigger(horse, sheep).
bigger(elephant, me).
\end{lstlisting}

\newpage
\begin{lstlisting}[caption=Naive Fibonnaci optimiert mit Assertions]
% Nach jeder Berechnung einer Fibonacci Zahl wird diese in die Wissensdatenbank an den Anfang (wichtig) gespeichert. Diese müssen nun nicht mehr neu berechnet werden.
:- dynamic fib_as/2.
fib_as(0, 0). 				% base case 1
fib_as(1, 1). 				% base case 2
fib_as(N, F) :- 			% general rule
	N > 1, 					% allow no negative numbers
	N1 is N-1, 
	N2 is N-2,
	fib_as(N1, F1), 		% calculate F1 = fib(N-1)
	fib_as(N2, F2), 		% calculate F2 = fib(N-2)
	F is F1+F2,
	asserta(fib_as(N, F)). 	% assert new fact
\end{lstlisting}

\section{Listen}
In Prolog gibt es Listen als Datenstrukturen. Eine Liste ist eine endliche Sequenz von Elementen. Listen werden mit eckigen Klammern dargestellt. Die Länger einer Liste ist die Anzahl darin enthaltenen Elemente. Listen-Elemente sind beliebige Terme. Die leere Liste [] ist eine spezielle Liste.

\begin{lstlisting}[caption=Listen in Prolog]
?- X = [a, b, c].
X = [a, b, c].
?- Y = [d, e, f(X), [x, y]].
Y = [d, e, f(X), [x, y]].
?- Z = [].
Z = [].
\end{lstlisting}

Die nicht-leeren Listen bestehen immer aus dem ersten Element (Kopf=head) und den restlichen Elementen (Schwanz=tail). Der Schwanz ist wiederum eine Liste. Somit sind Listen rekursiv aufgebaut.

\begin{lstlisting}[caption=Tail-Operator auf Listen]
?- [a, b, c] = [Head | Tail].
Head = a,
Tail = [b, c].

% Dies sind alles equivalente Terme.
?- L = [a | [b, c]], L = [a, b | [c]], L = [a, b, c | []].
L = [a, b, c].

% Hier interessiert nur zweites und drittes Element.
?- [_, X2, X3 | _] = [a, b, c, d, e, f].
X2 = b,
X3 = c.
\end{lstlisting}

\newpage
\subsection{Listenzugehörigkeit}
Ist X ein Element der Liste L? X kommt in der Liste vor, wenn X gleich Kopf der Liste ist oder wenn es im Schwanz der Liste vorkommt. 

\begin{lstlisting}[caption=Listenzugehörigkeit]
mem(X, [X | _]). 					% tail doesnt matter
mem(X, [_| Tail]) :- mem(X, Tail). 	% head doesnt matter

% Anmerkung: Wenn auf der linken Seite Variabeln definiert werden, welche auf der rechten Seite nicht verwendet werden, dann kommt es zur Warnung: Singleton Variables.

?- mem(b, [a, b, c]).
true.

?- mem(d, [a, b, c]) .
false.

?- mem([b, c], [a, [b, c]]).
true.

?- mem(X, [a, b, c]).
X = a;
X = b;
X = c;
false.

% Generiert unendlich lange Lösungslisten...
?- mem(a, L), mem(b, L), mem(c, L). % which list L contains a, b, & c?
L = [a, b, c|_G2239];
L = [a, b, _G2238, c|_G2242];
L = [a, b, _G2238, _G2241, c|_G2245];
...

% So wäre Permutation möglich. Die Länge der Liste beschränken!
?- L = [_, _, _], mem(a, L), mem(b, L), mem(c, L).
L = [a, b, c];
L = [a, c, b];
L = [b, a, c];
L = [c, a, b];
L = [b, c, a];
L = [c, b, a];
false.

% P.S. In Prolog gibt es bereits das Prädikat member/2.
\end{lstlisting}

\newpage
\subsection{Listenkonkatenation}
Wir wollen wissen, ob Liste L1 und Liste L2 zusammenhängt Liste L3 ergeben. Einfacher Fall: Wenn die L1 eine leere Liste ist, dann muss L2 und L3 gleich sein. Falls die erste Liste nicht leer ist, hat diese einen Kopf und Schwanz [X, L1]. Die Konkenation muss den gleichen Kopf haben, also [X, L3].

\begin{lstlisting}[caption=Listenkonkatenation]
conc([], L, L).
conc([X | L1], L2, [X | L3]) :- conc(L1, L2, L3).

% Zwei Listen konkatenieren
?- conc([a, b], [c, d], L).
L = [a, b, c, d].
?- conc([a, b], [c], [a, b, c, d]) .
false.
?- conc([a, [b, c], d], [a, [], b], L).
L = [a, [b, c], d, a, [], b].

% Zerlegung einer Liste in alle möglichen Teillisten
?- conc(L1, L2, [a, b, c]).
L1 = [],
L2 = [a, b, c];
L1 = [a],
L2 = [b, c];
L1 = [a, b],
L2 = [c];
L1 = [a, b, c],
L2 = [];
false.
\end{lstlisting}

\newpage
\section{Reihenfolge von Regeln und Zielen}
Die Reihenfolgen der Fakten und Regeln in der Wissensdatenbank spielt ein Rolle. Auch die Reihenfolge der Ziele von Regeln.

\begin{lstlisting}[caption=Reihenfolgen von Regeln und Zielen]
% Bekanntes is_bigger/2 Prädikat:
bigger(elephant, horse). 			% fact 1
bigger(horse, dog). 				% fact 2
bigger(horse, sheep). 				% fact 3
is_bigger(X, Y) :- bigger(X, Y). 	% rule 1
is_bigger(X, Y) :- 					% rule 2
	bigger(X, Z), 					% rule 2, goal 1
	is_bigger(Z, Y). 				% rule 2, goal 2

% Rule 1 und Rule 2 vertauscht (Reihenfolge von Klauseln vertauscht):
bigger(elephant, horse). 	% fact 1
bigger(horse, dog). 		% fact 2
bigger(horse, sheep). 		% fact 3
ib_1(X, Y) :- 				% rule 1
	bigger(X, Z), 			% rule 1, goal 1
	ib_1(Z, Y). 			% rule 1, goal 2
ib_1(X, Y) :- bigger(X, Y). % rule 2
% Die Lösung funktioniert, jedoch verwendet der resultierende Suchbaum viel mehr Backtracking - ineffizienter. Dies kann also die Effizienz von Prozeduren verschlechtern.

% Rule 1 und Rule 2 vertauscht (Reihenfolge von Regeln und Zielen vertauscht):
bigger(elephant, horse). 	% fact 1
bigger(horse, dog). 		% fact 2
bigger(horse, sheep). 		% fact 3
ib_2(X, Y) :- 				% rule 1
	ib_2(Z, Y), 			% rule 1, goal 1
	bigger(X, Z). 			% rule 1, goal 2
ib_2(X, Y) :- bigger(X, Y). % rule 2
% Kann die Anfragen nicht mehr beantworten: ERROR Out of local stack. Es wird endlos ib_2(_X, dog) aufgerufen bei der Anfrage von ib_2(elephant, dog). Änderung der Reihenfolge von Regeln und Zielen kann zu unbrauchbaren Programmen führen.
\end{lstlisting}

Es sollten zuerst immer die einfachen Dinge probiert werden. Typischerweise zuerst die Rekursionsbasis (einfacher Fall) und dann erst den allgemeinen Fall. 

\newpage
\section{Konjunktion und Diskonjunktion}
Beim definieren von Regeln wird nach :- die Ziele definiert. Einzelne Ziele werden dabei durch Komma getrennt, welches eine AND-Verknüpfung also einer Konjunktion entsprechen. Die Regel wird mit dem Punkt-Operator abgeschlossen. Auch Anfragen können verUNDet werden. Disjunktion (ODER) können mit Semikolon realisiert werden. Konjunktion und Disjunktion sind handelsübliche Prädikate. 

\begin{lstlisting}[caption=Disjunktion]
rich(tom). 			% fact 1
nice(mary). 		% fact 2
interesting(X) :- 	% rule 1
	rich(X); 		% rule 1, goal 1 OR
	nice(X).		% rule 1, goal 2

?- interesting(tom).
true .
?- interesting(mary).
true.

?- X = a; X = b.
X = a 				% pressing ;
X = b.

% Best Practices: Wir verwenden keine Disjunktion. Denn diese sind immer equivalent zu zwei Regeln mit den beiden Operanden der Disjunktion. Programme sind so einfacher zu verstehen.
interesting1(X) :- rich(X); nice(X). 	% disjunction
interesting2(X) :- rich(X). 			% rule 1
interesting2(X) :- nice(X). 			% rule 2
\end{lstlisting}

\newpage
\section{CUT-Operator}
Das Backtracking ist für Prolog charakteristisch. Dies kann aber oft zu Ineffizienzen führen. Prolog verschwendet Zeit und Speicher für Dinge die nirgends hinführen. Manchmal wäre etwas Kontrolle wünschenswert, dabei geht aber die vollständige deklarative Bedeutung verloren.

Mittels dem CUT-Operator \emph{!/0} kann Einfluss aufs Backtracking genommen werden. Dieser nimmt kein Argument, ist immer erfüllt und bindet die bisherige Wahl innerhalb der aktuellen Klausel.

\begin{lstlisting}[caption=Beispiel CUT-Operator]
% Wenn in folgender Regel der CUT erreicht wird. Bindet uns das zur linken Seite, nämlich zur spezifischen Klausel p und zu der Wahl b. Es bindet uns jedoch nicht zur rechten Seite - im Beispiel zur Wahl c.
p(X):- b(X), !, c(X).
\end{lstlisting}

\begin{figure}[h!]
\centering
\includegraphics[width=0.6\linewidth]{fig/prolog-suchbaum-cut}
\caption{Prolog Suchbaume mit und ohne CUT}
\label{fig:prolog-suchbaum-cut}
\end{figure}

Wenn CUTS implementiert werden, wird zwischen RED-CUT und GREEN-CUT unterschieden. GREEN-CUTS verändern die deklarative Bedeutung von Programmen nicht. Damit können Programme optimiert werden. RED-CUTS verändern nicht nur die prozedurale Bedeutung sondern auch die deklarative. RED-CUTS mit Sorgfalt verwenden, diese können nämlichen zu subtilen Programmierfehlern führen und können schwer zu verstehen sein.


\section{Negation von Prädikaten}

In Prolog kann eine Negation entweder mit einem Cut-Operator (\verb|!/0|) kombiniert mit \verb|fail/0| oder mit \verb|not/1| ausgedrückt werden. Als Beispiel verwenden folgende Aussage \textit{Mary mag alle Tiere ausser Schlangen}. Listing \ref{lst:negation} zeigt das obengennante Beispiel.

\begin{lstlisting}[caption=Negation, label=lst:negation]
% Wenn X eine Schlange unterbreche Backtracking und Regel ist falsch
likes(mary, X) :- snake(X), !, fail.
% Mit not/1
likes(mary, X) :- animal(X), not(snake(X)).
% Mary mag alle Tiere
likes(mary, X) :- animal(X).
\end{lstlisting}

Für \verb|not/1| gibt es noch eine alternative Syntax \verb|\+/1|, die aber die gleiche Funktion hat. \verb|not/1| ist in Prolog als \textit{schwache Negation} (negation as failure) definiert. Schwache Negation basiert auf der Closed-World-Assumption (CWA) und nicht auf der Negation in der mathematischen Logik. In Prolog wird angenommen, dass jedes Programm alles Wahre über die Welt beschreibt. Deshalb wird alles was nicht im Programm beschrieben ist als falsch angenommen. Deshalb wird die Negation von Dingen, die das Programm nicht weiss immer als wahr angenommen (schwache Negation - CWA). Listing \ref{lst:cwa} zeigt das Prolog findet die Welt ist nicht rund, obwohl sie dass eigentlich ist. Dadurch können in Prolog komische Resultate entstehen.

\begin{lstlisting}[caption=CWA, label=lst:cwa]
round(ball)

?- round(ball).
true. % as expected

?- round(earth).
false. % caused by the CWA

?- not(round(earth)).
true. % caused by negation as failure
\end{lstlisting}

Zudem sollte man darauf achten, dass die Variable welche \verb|not(X)| übergeben wird vorher immer instanziert ist. Sonst kann es zu unerwünschten Nebeneffekten kommen. Wie folgendes Beispiel verdeutlicht:

\begin{lstlisting}[caption=Not muss gebunden sein]
great_food(mekong).
great_food(eule).
limited_seating(mekong).
good_place(Restaurant) :-
	not(limited_seating(Restaurant)).
	
?- great_food(X), good_place(X). 	% query 1
X = eule. 							% as expected

?- good_place(X), great_food(X). 	% query 2
false. 								% what went wrong?

% Im ersten Beispiel wird X bei great_food für good_place gebunden.
% Im zweiten Beispiel wird X an good_place ungebunden übergeben.
\end{lstlisting}

Das Problem liegt bei der Quantifikation von Variablen. Prolog ändert die Quantifikation von existentiell zu universell. Sind die Variablen nicht gebunden (dürfen nicht mehr variabel sein) wird diese Umwandlung gemacht.

\section{Constraint Logik Programmierung}

In Prolog können Constraint-Satisfaction-Probleme (CSP) relativ einfach mit Constraint Logik Programming (CLP) gelöst werden. Ein CSP muss über folgende drei Eigenschaften verfügen:
\begin{enumerate}
	\item Eine Menge von Variablen
	\item Domänen, aus welchen die Variablen Werte annehmen können
	\item Constraints (Bedingungen) welche die Variablen erfüllen müssen
\end{enumerate}
Vereinfacht gesagt setzt CLP alle Werte der gegebenen Domäne in die Variablen ein bis die Bedingungen erfüllt sind. Damit CLP funktioniert muss eine Bibliothek (z.B. \verb|use_module(library(clpr)).|) importiert werden, welche die Domäne darstellt. Die entsprechende Bedingung muss in gschweifte Klammern geschrieben werden (z.B. \verb|{ X + 1 = 5}|). In Prolog stehen die folgenden drei Domänen zur Verfügung:
\begin{description}
	\item[CLP-R:] Alle Variablen müssen reelle Zahlen annehmen
	\item[LP-Q:] Alle Variablen müssen rationale Zahlen annehmen
	\item[CLP-FD] Alle Variablen müssen Zahlen aus einem selbst definierten Wertebereich annehmen.
\end{description}
Bei CLP-FD können die Domänen (Wertebereiche) programmatisch definiert werden. Dabei sind folgende Prädikate wichtig:
\begin{description}
	\item[in/2:] Legt den Wertebereich einer Variablen fest
	\item[ins/2:] Legt den Wertebereich einer Liste von Variablen fest
	\item[all\_distinct/1:] stellt sicher, dass Variablen in einer Liste paarweise unterschiedliche Werte haben
	\item[label/1:] Weist allen Variablen einer Liste Werte zu (dadurch werden systematisch Lösungen für das gegebene Problem gesucht)
\end{description}
Zudem haben alle Vergleichsoperatoren den Präfix \verb|#| (z.B. \verb|#>|, \verb|#=|). Listing \ref{lst:clp-fd} zeigt einige Beispiele zu den oben genannten Prädikaten.
\begin{lstlisting}[caption=CLP-FD, label=lst:clp-fd]
?- use_module(library(clpfd)).
% ... (output omitted here)
true.

?- X in 5..9, X #> 8.
X = 9.

?- [X, Y] ins 1..2, X #=< 1, Y #\= 1.
X = 1,
Y = 2.

?- [X, Y] ins 1..2, all_distinct([X, Y]).
X in 1..2,
all_distinct([X, Y]),
Y in 1..2.

?- [X, Y] ins 1..2, all_distinct([X, Y]), label([X, Y]).
X = 1,
Y = 2 % press ;
X = 2,
Y = 1.
\end{lstlisting}

\begin{lstlisting}[caption=Beispiel ohne CLP]
convert(Celsius, Fahrenheit) :-
	Celsius is (Fahrenheit - 32)*5/9.

?- convert(C, 95).
C = 35.

?- convert(35, F).
ERROR: is/2: Arguments are not sufficiently instantiated
\end{lstlisting}

\begin{lstlisting}[caption=Beispiel mit CLP]
:- use_module(library(clpr)).
convert_clp(Celsius, Fahrenheit) :-
	{ Celsius = (Fahrenheit - 32)*5/9 }.

?- convert_clp(35, F).
F = 95.0 .

?- convert_clp(C, F).
{F=32.0+1.7999999999999998*C}.

; Auch Fibonacci in beide Richtungen wird durch CLP möglich.
\end{lstlisting}

\begin{lstlisting}[caption=Typische CLP Aufgabe]
% Theo ist 13 Jahre alt. In 3 Jahren ist der Grossvater doppelt so alt wie Theo's Vater und in 7 Jahren ist der Grossvater viermal so alt wie Theo. Wie
alt ist der Vater von Theo?

?- { T = 13, G+3 = 2*(V+3), G+7 = 4*(T+7) }.
T = 13.0, 	% current age of Theo
G = 73.0, 	% current age of Theos grandfather
V = 35.0 . 	% current age of Theos father
\end{lstlisting}

\begin{lstlisting}[caption=Zahlenrätsel]
send_more_money([S,E,N,D] + [M,O,R,E] = [M,O,N,E,Y]) :-
	Vars = [S,E,N,D,M,O,R,Y], % define the variables
	Vars ins 0..9, % define the domain for the vars
	all_distinct(Vars), % all variables must be different
	S*1000 + E*100 + N*10 + D +
	M*1000 + O*100 + R*10 + E #= % attention: use #=/2
	M*10000 + O*1000 + N*100 + E*10 + Y, % addition must be ok
	M #\= 0, S #\= 0, % numbers cannot start with zero
	label(Vars). % assign values to the variable

?- send_more_money(As + Bs = Cs).
As = [9, 5, 6, 7],
Bs = [1, 0, 8, 5],
Cs = [1, 0, 6, 5, 2] .
\end{lstlisting}

\section{Sudoku}

In Prolog kann mittels CLP relativ einfach beliebige Sudokus gelöst werden. Listing \ref{lst:sudoku} zeigt das gesamte Programm. Wenn in jedem Feld eine anonyme Variable übergeben wird, dann returniert Prolog jedes mögliche Sudoku.

\begin{lstlisting}[language=Prolog, caption=Sudoku, label=lst:sudoku]
:- use_module(library(clpfd)).

sudoku(Rows) :-
	% Fasst alle Listen in Rows zu einer gesamten Liste Vs zusammen
	append(Rows, Vs), 
	% Domäne festlegen
	Vs ins 1..9,
	% Wendet all_distinct auf alle Elemente der Liste Rows an (nur eine Ziffer pro Zeile)
	maplist(all_distinct, Rows),
	% Transponiert die Liste Rows zu Liste Columns
	transpose(Rows, Columns),
	% Wendet all_distinct auf alle Elemente der Liste Columns an (nur eine Ziffer pro Spalte)
	maplist(all_distinct, Columns),
	% Weist die 9 Zeilen in Rows den Variablen A-I zu
	Rows = [A, B, C, D, E, F, G, H, I],
	% Prüft ob nur eine Ziffer pro Block. Wird 3x aufgerufen!
	blocks(A, B, C), 
	blocks(D, E, F), 
	blocks(G, H, I),
	% Wendet label auf alle Elemente der Liste Rows an (Lösungssuche beginnt)
	maplist(label, Rows).

blocks([], [], []).
% Prüfe die ersten drei Elemente der drei Zeilen und danach der Rest rekursiv
blocks([A, B, C|Bs1], 
	   [D, E, F|Bs2], 
	   [G, H, I|Bs3]) :-
	all_distinct([A, B, C, D, E, F, G, H, I]),
	blocks(Bs1, Bs2, Bs3).
\end{lstlisting}

\section{HTTP/Json}

Prolog bietet diverse Bibliotheken um HTTP-Anfragen abzuschicken. Listing \ref{lst:http} zeigt ein Beispiel für eine GET- bzw. POST-Anfrage.
 
\begin{lstlisting}[language=Prolog, caption=HTTP, label=lst:http]
?- use_module(library(http/http_client)).
?- use_module(library(http/http_json)).

?- http_get('http://wherever.ch/pcp.txt', Reply, []).
Reply = 'Hallo zusammen! Dies ist eine Test-Datei fuer das PCPModul, abgelegt unter http://wherever.ch/pcp.txt. - Prolog rockt! :-) MfG, Ruedi Arnold'.

?- http_post('http://localhost:16316/test',json(['say hi to http post']), SolutionResponse, []).
SolutionResponse = 'PCPProblemProvider server up and running at http://localhost:16316/, reached via HTTP POST - POSTed data: ["say hi to http post" ]'.
\end{lstlisting}

JSON-Objekte werden in Prolog in Terme der Form \verb|json([...])| abgepackt, wobei in diese Prolog-Liste jeweils Paare von \verb|Name=Value| kommen (z.B. \verb|{ institution: hslu, dept: t&a }| wird zu \verb|json([institution=hslu, dept=t&a])|).
\verb|json()| ist also praktisch ein \textit{Markier}-Prädikat (marker predicate) und bietet sonst keine Funktionalität.

\section{Eingebaute Prädikate}

\begin{description}
	\item[write(+Term)] Mittels write('hallo') kann ein Term auf die Konsole geschrieben werden.
	\item[read(-Term)] Kann ein Term von der Konsole eingelesen werden.
\end{description}